// Apply the java plugin to add support for Java
plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.hidetake.swagger.generator' version '2.4.2'
    id 'org.springframework.boot' version '2.0.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'jacoco'
}

version = '0.5'
mainClassName = 'nl.esciencecenter.computeservice.rest.Application'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    jcenter()
    maven { url "https://raw.github.com/weddini/spring-boot-throttling/mvn-repo/" }
    maven { url 'https://jitpack.io'}
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Xenon
    compile group: 'nl.esciencecenter.xenon', name: 'xenon', version: '2.2.0'

    //Spring(boot)
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.0.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.0.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: '2.0.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version: '2.0.4.RELEASE'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.5.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.5.0'

    //Jackson, match version used in Spring
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.0'
	
    //compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
	
	//JPA, hibernate and h2 database persistance libraries
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.0.4.RELEASE'
	runtime group: 'com.h2database', name: 'h2', version: '1.4.196'
	
	// Security
	compile group: 'com.weddini.throttling', name: 'spring-boot-throttling-starter', version: '0.0.9'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.0.4.RELEASE'
	
	// Admin Interface

    //Swagger
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.2'

    //Misc
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    
    //cli
    compile group: 'com.beust', name: 'jcommander', version: '1.71'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    
    testCompile 'junit:junit:4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.4.RELEASE'
}

// Include some more files in the distribution zip
// Don't use include an extra directory in the dist zip.
distributions {
    main {
        contents {
			
			from(['config/config.yml', 'config/application.properties']) {
				into '/config'
			}
			from('/test_setup') {
				into '/test_setup'
			}
			from('/docs') {
				into 'docs'
			}
			from (['README.md',
				   'LICENSE',
				   'xenon-workflow-execution-api.yaml']) {
				into '/'
			} 
			into '/'
        }
    }
}

task createAdminStartScript(type: CreateStartScripts) {
	mainClassName = "nl.esciencecenter.computeservice.cli.XenonflowCLI"
	classpath = startScripts.classpath
	outputDir = startScripts.outputDir
	applicationName = "xenonflow-admin"
}

applicationDistribution.into("bin") {
	from (createAdminStartScript)
}

jar {
    manifest {
        // Make sure the app's jar can be used with a simple -cp
        attributes(
        	'Class-Path': configurations.runtime.collect { "${it.getName()}" }.join(' ')
        )
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/reports/coverage")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage/jacocoHtml")
    }
}

validateSwagger {
    inputFile = file('xenon-workflow-execution-api.yaml')
}

def swaggerProperties = [
        'basePackage'            : 'nl.esciencecenter.computeservice.rest',
        'modelPackage'           : 'nl.esciencecenter.computeservice.rest.model',
        'apiPackage'             : 'nl.esciencecenter.computeservice.rest.api',
        'invokerPackage'         : 'nl.esciencecenter.computeservice.rest.invoker',
        'java8'                  : 'true',
        'hideGenerationTimestamp': 'true',
        'dateLibrary'            : 'java8',
        'serializableModel'      : 'true'

]

generateSwaggerCode {
    language = 'spring'
    inputFile = file('xenon-workflow-execution-api.yaml')
    additionalProperties = swaggerProperties
//    outputDir = file('src')
//    configFile = file('config.json')
//    components = ['models', 'apis']
}
