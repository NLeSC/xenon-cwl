// Apply the java plugin to add support for Java
plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.hidetake.swagger.generator' version '2.4.2'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'jacoco'
    id "com.github.node-gradle.node" version "2.2.2"
}

version = '0.7'
mainClassName = 'nl.esciencecenter.computeservice.rest.Application'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    jcenter() 
    maven { url "https://raw.github.com/weddini/spring-boot-throttling/mvn-repo/" }
    maven { url 'https://jitpack.io'}
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Xenon
    compile group: 'nl.esciencecenter.xenon', name: 'xenon', version: '3.0.6'
    compile group: 'nl.esciencecenter.xenon.adaptors', name: 'xenon-adaptors-cloud', version: '3.0.2'

    //Spring(boot)
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.2.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: '2.2.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version: '2.2.4.RELEASE'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

    //Jackson, match version used in Spring
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.8'
	
    //compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
	
	//JPA, hibernate and h2 database persistance libraries
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.2.4.RELEASE'
	runtime group: 'com.h2database', name: 'h2', version: '1.4.199'
	
	// Security
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.2.4.RELEASE'
	
	// Admin Interface

    //Swagger
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.2'

    //Misc
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    
    //cli
    compile group: 'com.beust', name: 'jcommander', version: '1.71'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    
    testCompile group: 'junit', name:'junit', version:'4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.2.4.RELEASE'
}

// Include some more files in the distribution zip
// Don't use include an extra directory in the dist zip.
distributions {
    main {
        contents {
			
			from(['config/config.yml', 'config/application.properties']) {
				into '/config'
			}
			from('/test_setup') {
				into '/test_setup'
			}
			from('/docs') {
				into 'docs'
			}
			from (['README.md',
				   'LICENSE',
				   'xenon-workflow-execution-api.yaml']) {
				into '/'
			} 
			into '/'
        }
    }
}

task createAdminStartScript(type: CreateStartScripts) {
	mainClassName = "nl.esciencecenter.computeservice.cli.XenonflowCLI"
	classpath = startScripts.classpath
	outputDir = startScripts.outputDir
	applicationName = "xenonflow-admin"
}

applicationDistribution.into("bin") {
	from (createAdminStartScript)
}

jar {
    manifest {
        // Make sure the app's jar can be used with a simple -cp
        attributes(
        	'Class-Path': configurations.runtime.collect { "${it.getName()}" }.join(' ')
        )
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/reports/coverage")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage/jacocoHtml")
    }
}

validateSwagger {
    inputFile = file('xenon-workflow-execution-api.yaml')
}

def swaggerProperties = [
        'basePackage'            : 'nl.esciencecenter.computeservice.rest',
        'modelPackage'           : 'nl.esciencecenter.computeservice.rest.model',
        'apiPackage'             : 'nl.esciencecenter.computeservice.rest.api',
        'invokerPackage'         : 'nl.esciencecenter.computeservice.rest.invoker',
        'java8'                  : 'true',
        'hideGenerationTimestamp': 'true',
        'dateLibrary'            : 'java8',
        'serializableModel'      : 'true'

]

generateSwaggerCode {
    language = 'spring'
    inputFile = file('xenon-workflow-execution-api.yaml')
    additionalProperties = swaggerProperties
//    outputDir = file('src')
//    configFile = file('config.json')
//    components = ['models', 'apis']
}

def webappDir = "$projectDir/src/main/frontend"

node {
  // Version of node to use.
  version = '12.16.1'
  // Version of npm to use.
  npmVersion = '6.13.7'
  // Version of Yarn to use.
  yarnVersion = '1.22.0'
  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  download = true
  // Set the work directory for unpacking node
  workDir = file("${webappDir}/nodejs")
  // Set the work directory for NPM
  npmWorkDir = file("${webappDir}/npm")
  // Set the work directory for Yarn
  yarnWorkDir = file("${webappDir}")
  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${webappDir}")
}

sourceSets {
	main {
		resources {
			srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
		}
	}
}

processResources {
	dependsOn "buildAngular"
}

task buildAngular(type: YarnTask) {
  dependsOn "yarn"
  args = ['run', 'build']
  inputs.dir "$webappDir"
  outputs.dir("$webappDir/dist/xenonflow-admin")
}
